bits 16

section _TEXT class=CODE

;
; void _cdecl x86_div64_32(uint64_t dividend, uint32_t divisor, uint64_t* quotientOut, uint32_t* remainderOut);
;
global _x86_div64_32
_x86_div64_32:

    ; make new call frame
    push bp             ; save old call frame
    mov bp, sp          ; initialize new call frame

    push bx

    ; divide upper 32 bits
    mov eax, [bp + 8]   ; eax <- upper 32 bits of dividend
    mov ecx, [bp + 12]  ; ecx <- divisor
    xor edx, edx
    div ecx             ; eax - quot, edx - remainder

    ; store upper 32 bits of quotient
    mov bx, [bp + 16]
    mov [bx + 4], eax

    ; divide lower 32 bits
    mov eax, [bp + 4]   ; eax <- lower 32 bits of dividend
                        ; edx <- old remainder
    div ecx

    ; store results
    mov [bx], eax
    mov bx, [bp + 18]
    mov [bx], edx

    pop bx

    ; restore old call frame
    mov sp, bp
    pop bp
    ret


;
; int 10h ah=0Eh
; args: character, page
;

global _x86_Video_WriteCharTeletype
_x86_Video_WriteCharTeletype:

    ;make new call frame
    push bp                            ; save old call frame
    mov bp, sp                        ; initialize new call frame

    ; save bx
    push bx

    ; [bp + 0] - return address (small memory model => 2 bytes)
    ; [bp + 2] - first argument (character); bytes are converted to words (you can't push a single byte on the stack)
    ; [bp + 4] - second argument (page)

    mov ah, 0Eh
    mov al, [bp + 4]
    mov bh, [bp + 6]

    int 10h

    ;restore bx
    pop bx

    ; restore old call fame
    mov sp, bp
    pop bp
    ret

 